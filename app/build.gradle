plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-kapt'
    id "com.mikepenz.aboutlibraries.plugin" version "10.5.2"
}

apply plugin: 'com.mikepenz.aboutlibraries.plugin'

android {
    namespace 'com.starry.greenstash'
    compileSdk 33

    defaultConfig {
        applicationId "com.starry.greenstash"
        minSdk 24
        targetSdk 33
        versionCode 27
        versionName "2.7.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation":
                                      "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            // Rename the output apk file automatically.
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def name = "GreenStash-v${variant.versionName}.apk"
                    output.outputFileName = name
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
        coreLibraryDesugaringEnabled true
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        buildConfig = true
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.7'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

aboutLibraries {
    // Remove the "generated" timestamp to allow for reproducible builds
    excludeFields = ["generated"]
}


dependencies {

    // Android core components.
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.activity:activity-compose:1.7.2'
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.28.0"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.6.1"
    // Jetpack compose.
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.material3:material3:1.1.1"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.animation:animation:$compose_version"
    implementation "com.google.accompanist:accompanist-navigation-animation:0.29.0-alpha"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.6.1"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    // Material theme for main activity.
    implementation 'com.google.android.material:material:1.9.0'
    // Android 12+ splash API.
    implementation 'androidx.core:core-splashscreen:1.0.1'
    // Room database
    implementation "androidx.room:room-ktx:2.5.2"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    kapt "androidx.room:room-compiler:2.5.2"
    androidTestImplementation "androidx.room:room-testing:2.5.2"
    // Room database backup library.
    implementation 'de.raphaelebner:roomdatabasebackup:1.0.0-beta12'
    // Dagger - Hilt.
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    kapt "androidx.hilt:hilt-compiler:1.0.0"
    // DataStore Preferences
    implementation("androidx.datastore:datastore-preferences:1.0.0")
    // Coil Image loading library.
    implementation "io.coil-kt:coil-compose:2.4.0"
    // Material 3 calender / Date picker.
    implementation 'com.maxkeppeler.sheets-compose-dialogs:core:1.1.0'
    implementation 'com.maxkeppeler.sheets-compose-dialogs:calendar:1.1.0'
    // Lottie animations.
    implementation "com.airbnb.android:lottie-compose:4.1.0"
    // Bio-metric authentication.
    implementation "androidx.biometric:biometric:1.1.0"
    // Open Source Libraries Screen.
    implementation "com.mikepenz:aboutlibraries-core:10.5.2"
    implementation "com.mikepenz:aboutlibraries-compose:10.5.2"
    // Crash Handler.
    implementation 'cat.ereza:customactivityoncrash:2.4.0'
    // Oreo back-ports for API 24 (N)
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'
    // Testing components.
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
}